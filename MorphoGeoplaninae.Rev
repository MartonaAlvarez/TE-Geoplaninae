##This is a script to run revbayes with morphological matrix for the analyses of Lago-Barcia et al 2021

### Created on Wed March 10 12:00 2021
### @author: Marta √Ålvarez-Presas  marta.alvarez-presas@bristol.ac.uk


##Load Data Matrix

morpho <- readDiscreteCharacterData("data/MatrixMorphoDomingov2.nex")

##Create Helper variables

taxa <- morpho.names()
num_taxa <- morpho.size()
num_branches <- 2 * num_taxa - 2

moves = VectorMoves()
monitors = VectorMonitors()

##The Mk Model

br_len_lambda ~ dnExp(0.2)
moves.append( mvScale(br_len_lambda, weight=2) )

phylogeny ~ dnUniformTopologyBranchLength(taxa, branchLengthDistribution=dnExp(br_len_lambda))
moves.append( mvNNI(phylogeny, weight=num_branches/2.0) )
moves.append( mvSPR(phylogeny, weight=num_branches/10.0) )
moves.append( mvBranchLengthScale(phylogeny, weight=num_branches) )

tree_length := phylogeny.treeLength()

Q_morpho <- fnJC(4)

alpha_morpho ~ dnUniform( 0, 1E8 )
rates_morpho := fnDiscretizeGamma( alpha_morpho, alpha_morpho, 4 )

#Moves on the parameters of the gamma distribution
moves.append( mvScale(alpha_morpho, lambda=1, weight=2.0) )

phyMorpho ~ dnPhyloCTMC(tree=phylogeny, siteRates=rates_morpho, Q=Q_morpho, type="Standard", coding="variable")
phyMorpho.clamp(morpho)

#Complete MCMC Analysis

mymodel = model(phylogeny)

##Specify Monitors and Output Filenames

monitors.append( mnModel(filename="output/mk.log", printgen=1000) )
monitors.append( mnFile(filename="output/mk.trees", printgen=1000, phylogeny) )
monitors.append( mnScreen(printgen=1000) )

##Set-Up the MCMC

mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")
mymcmc.run(generations=200000, tuningInterval=2000)

q()
